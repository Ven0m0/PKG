# Maintainer: ven0m0
# Merged from: vscodium-electron & local patches
_pkgname=vscodium
_electron=electron38
_nodejs="22.15.1"
pkgname=${_pkgname}-electron
pkgver=1.106.27818
pkgrel=1
pkgdesc="VS Code (System Electron) + MS Marketplace + Translucency + Fixes"
arch=('x86_64' 'aarch64' 'armv7h')
url='https://github.com/VSCodium/vscodium.git'
license=('MIT')
options=(!strip !debug !emptydirs)
depends=("$_electron" 'libsecret' 'libx11' 'libxkbfile' 'ripgrep')
optdepends=(
  'gvfs: Trash support'
  'libdbusmenu-glib: KDE global menu'
)
makedepends=('git' 'python' 'jq' 'rustup' 'fnm' 'bun' 'nvm')
provides=('codium' 'vscodium' 'code')
conflicts=('codium' 'vscodium' 'vscodium-bin' 'vscodium-git' 'code')
source=(
  "git+${url}#depth=1#tag=${pkgver}"
  "${_pkgname}-electron.patch"
  "translucent.patch"
  "microphone.patch"
  "product.json"
  "${_pkgname}.sh"
  "${_pkgname}.js"
  "${_pkgname}.desktop"
  "${_pkgname}-uri-handler.desktop"
)
sha256sums=('SKIP'
  '338d678e199b53f06d10ac9c82567c4baceb7ff3bd3c0017d4b2ef4a801ae99a'
  '2aa74b7477edb3517d7afb1294aae96ee3341e9d85ce0872e2e509ff8a70c02a'
  'cec939c5a41c34a5677086e171279372e6c1e80eb5d12b7241f6a1b7dc5f6a29'
  'e16070daa44d9bfdf70e6f68392a4fab359e8cd05493f80c0b9ea33d8030db83'
  'fc63453bb4a5deb880c4791eaab3018a309c92f6d8ec1209857de82d66ac1857'
  '5340ae24cda3b558a970b9460152111214d2e1ec0f3855beab31d214481fac35'
  '3a5bc109974fcf408855c13965f6d6be0997655c5b359de0bfd19a678c00844e'
  '6eef345b65bf2679c928c763529540435ab9c6e1836917319810a7a2d484ae1b')
case "$CARCH" in
x86_64) _vscode_arch=x64 ;;
aarch64) _vscode_arch=arm64 ;;
armv7h) _vscode_arch=arm ;;
*) _vscode_arch=DUMMY ;;
esac
_ensure_node() {
  command -v fnm &>/dev/null && {
    eval "$(fnm env --use-on-cd)"
    fnm install "$_nodejs"
    fnm use "$_nodejs"
    return 0
  }
  [[ -f /usr/share/nvm/init-nvm.sh ]] && {
    command -v nvm >/dev/null && nvm deactivate && nvm unload
    export NVM_DIR="${srcdir}/nvm"
    source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
    nvm install "$_nodejs"
    nvm use "$_nodejs"
    return 0
  }
  printf "Error: neither fnm nor nvm found\n" >&2
  return 1
}

prepare() {
  command -v "$_electron" &>/dev/null || {
    echo "Error: $_electron not found" >&2
    exit 1
  }
  sed "s/@ELECTRON@/${_electron}/" "$srcdir/vscodium.sh" >"$srcdir/vscodium-electron.sh"
  sed "s/@ELECTRON@/${_electron}/" "$srcdir/vscodium.js" >"$srcdir/vscodium-electron.js"
  cd "$srcdir/vscodium"
  [[ -d vscode ]] && rm -rf vscode VSCode*
  patch -Nu build.sh -i "$srcdir/vscodium-electron.patch"
  cp "$srcdir/translucent.patch" patches/
  cp "$srcdir/microphone.patch" patches/
}

build() {
  _ensure_node
  export HOME="$srcdir" DISABLE_UPDATE="yes" DISABLE_V8_COMPILE_CACHE=0 SHOULD_BUILD="yes" SHOULD_BUILD_REH="no" CI_BUILD="no" OS_NAME="linux" VSCODE_PLATFORM="linux" VSCODE_ARCH="x64" VSCODE_QUALITY="stable" RELEASE_VERSION="$pkgver" CARGO_TARGET_DIR="${srcdir}/${_pkgname}/vscode/cli/target"
  if command -v bun &>/dev/null; then
    bun i -g node-gyp
    [[ -d "$HOME/.bun/bin" ]] && export PATH="$HOME/.bun/bin:$PATH"
  else
    npm install -g node-gyp
  fi
  rustup default stable
  cd "$srcdir/vscodium"
  . dev/build.sh
}

package() {
  local _dist="${srcdir}/${_pkgname}/VSCode-linux-${_vscode_arch}"
  install -d -m755 "${pkgdir}/usr/bin" "${pkgdir}/usr/lib/${_pkgname}" "${pkgdir}/usr/share/applications" "${pkgdir}/usr/share/pixmaps" "${pkgdir}/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/zsh/site-functions" "${pkgdir}/usr/share/bash-completion/completions"
  install -m644 "${srcdir}/product.json" "${_dist}/resources/app/product.json"
  cp -r --no-preserve=ownership --preserve=mode "${_dist}/resources/app"/* "${pkgdir}/usr/lib/${_pkgname}/"
  cp -r --no-preserve=ownership --preserve=mode "${_dist}/resources/completions" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${_dist}/resources/app/LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
  install -Dm644 "${_dist}/resources/app/ThirdPartyNotices.txt" -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
  rm "${pkgdir}/usr/lib/${_pkgname}/LICENSE.txt" "${pkgdir}/usr/lib/${_pkgname}/ThirdPartyNotices.txt"
  install -Dm755 "${srcdir}/vscodium-electron.sh" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm755 "${srcdir}/vscodium-electron.js" "${pkgdir}/usr/lib/${_pkgname}/vscodium.js"
  ln -s "/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/code"
  ln -s "/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/codium"
  install -Dm644 "${srcdir}/vscodium.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "${srcdir}/vscodium-uri-handler.desktop" "${pkgdir}/usr/share/applications/${_pkgname}-uri-handler.desktop"
  install -Dm644 "${_dist}/resources/app/resources/linux/code.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  ln -s "/usr/lib/${_pkgname}/completions/zsh/_codium" "${pkgdir}/usr/share/zsh/site-functions/_vscodium"
  ln -s "/usr/lib/${_pkgname}/completions/bash/codium" "${pkgdir}/usr/share/bash-completion/completions/vscodium"
  ln -sf /usr/bin/rg "${pkgdir}/usr/lib/${_pkgname}/node_modules/@vscode/ripgrep/bin/rg"
}
