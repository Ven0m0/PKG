# shellcheck enable=all shell=bash source-path=SCRIPTDIR external-sources=true
# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=webp-converter-git
pkgver=1.0.0.r2.gce5d96c
_electronversion=33
_nodeversion=20
pkgrel=1
pkgdesc="A batch of jpg, png format images converted to webp format tool.(Use system-wide electron)一款批量将 jpg、png 格式图像转换为 webp 格式的工具"
arch=('any')
url="https://github.com/HarrisonWang/webp-converter"
license=('MIT')
conflicts=("${pkgname%-git}")
provides=("${pkgname%-git}=${pkgver%.r*}")
depends=('libvips' "electron${_electronversion}")
makedepends=('gendesk' 'npm' 'git' 'curl')
optdepends=(
  'fnm: Fast Node Manager (preferred)'
  'nvm: Node Version Manager (fallback)'
)
source=(
  "${pkgname%-git}.git::git+${url}"
  "${pkgname%-git}.sh"
)
sha256sums=('SKIP'
            '291f50480f5a61bc9c68db7d44cd0412071128706baa868a9cb854f8779a1980')
has(){ command -v -- "$1" &>/dev/null; }
pkgver(){
  cd "${srcdir}/${pkgname%-git}.git" || return 1
  set -o pipefail
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g' || printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}
_ensure_node(){
  local node_version="${1:-$_nodeversion}"
  if has fnm; then
    export FNM_DIR="${srcdir}/.fnm"
    eval "$(fnm env --use-on-cd)"
    fnm install "$node_version" &>/dev/null || return 1
    fnm use "$node_version" || return 1
    printf 'Using fnm with node %s\n' "$node_version" >&2
  elif has nvm || [[ -s /usr/share/nvm/init-nvm.sh ]]; then
    export NVM_DIR="${srcdir}/.nvm"
    # shellcheck source=/dev/null
    source /usr/share/nvm/init-nvm.sh || return 1
    nvm install "$node_version" || return 1
    nvm use "$node_version" || return 1
    printf 'Using nvm with node %s\n' "$node_version" >&2
  else
    printf 'Error: Neither fnm nor nvm found\n' >&2
    return 1
  fi
}
prepare(){
  sed -i -e "s/@electronversion@/${_electronversion}/g;s/@appname@/${pkgname%-git}/g;s/@runname@/app.asar/g;s/@cfgdirname@/${pkgname%-git}/g;s/@options@/env ELECTRON_OZONE_PLATFORM_HINT=auto/g" "${srcdir}/${pkgname%-git}.sh"
  _ensure_node "$_nodeversion"
  gendesk -q -f -n --pkgname="${pkgname%-git}" --pkgdesc="${pkgdesc}" --categories="Graphics" --name="${pkgname%-git}" --exec="${pkgname%-git} %U"
  sed -i "3i\Name[zh_CN]=${_zhsname}" "${srcdir}/${pkgname%-git}.desktop"
  cd "${srcdir}/${pkgname%-git}.git" || return 1
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1 SYSTEM_ELECTRON_VERSION="$(electron${_electronversion} -v | sed 's/v//g')" HOME="${srcdir}/.electron-gyp"
  { printf '\n\ncache=%s\nmaxsockets=10\n' "${srcdir}/.npm_cache"; } >>.npmrc
  [[ $(curl -s ipinfo.io/country) == *CN* ]] && {
    { printf 'registry=https://registry.npmmirror.com\nelectron_mirror=https://registry.npmmirror.com/-/binary/electron/\nelectron_builder_binaries_mirror=https://registry.npmmirror.com/-/binary/electron-builder-binaries/\n'; } >>.npmrc
    find ./ -type f -name "package-lock.json" -exec sed -i "s/registry.npmjs.org/registry.npmmirror.com/g" {} +
  }
  sed -i "s/\"electron\": \"[^\"]*\"/\"electron\": \"${SYSTEM_ELECTRON_VERSION}\"/g" package.json
  NODE_ENV=development npm install --legacy-peer-deps --no-audit
}
build(){
  cd "${srcdir}/${pkgname%-git}.git" || return 1
  local electronDist="/usr/lib/electron${_electronversion}"
  NODE_ENV=production npm exec -c "electron-builder --linux dir -c.electronDist=${electronDist}"
}
package(){
  install -Dm755 "${srcdir}/${pkgname%-git}.sh" "${pkgdir}/usr/bin/${pkgname%-git}"
  install -Dm644 "${srcdir}/${pkgname%-git}.git/dist/linux-"*/resources/app.asar -t "${pkgdir}/usr/lib/${pkgname%-git}"
  cp -Pr --no-preserve=ownership "${srcdir}/${pkgname%-git}.git/dist/linux-"*/resources/app.asar.unpacked "${pkgdir}/usr/lib/${pkgname%-git}"
  install -Dm644 "${srcdir}/${pkgname%-git}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/${pkgname%-git}.git/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set sw=2 ts=2 et:
