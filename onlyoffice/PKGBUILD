# Maintainer: VolRen <VolRencs@proton.me>

pkgname=onlyoffice
pkgver=9.1.0
pkgrel=1
pkgdesc='An office suite that combines text, spreadsheet and presentation editors'
arch=('x86_64')
url='https://www.onlyoffice.com/'
license=('AGPL-3.0-only')

depends=(
  'curl'
  'gtk3'
  'gstreamer'
  'gst-plugins-base-libs'
  'libxss'
  'nss'
  'nspr'
  'desktop-file-utils'
  'hicolor-icon-theme'
)

optdepends=(
  'gst-plugins-good: for better video playback support'
  'gst-libav: for additional multimedia codecs'
  'libpulse: for audio playback support'
  'alsa-lib: for audio output via ALSA'
  'otf-takao: for Japanese fonts'
  'ttf-ms-fonts: for Microsoft fonts'
  'libreoffice: for OpenSymbol fonts'
)

provides=("onlyoffice=${pkgver}")
conflicts=('onlyoffice')
options=('!strip' '!emptydirs')
source=("onlyoffice-desktopeditors-${CARCH}-${pkgver}.deb"::"https://github.com/ONLYOFFICE/DesktopEditors/releases/download/v${pkgver}/onlyoffice-desktopeditors_amd64.deb"
"onlyoffice.svg")
noextract=("onlyoffice-desktopeditors-${CARCH}-${pkgver}.deb")
sha256sums=('0f7e84ee16024c9f4bc3d5e707c9f13063090c946133e2be6ef8ae33dbaece19'
            '0efe15ee5ae7c01fd5cb3198bc849371ebc12ce48e0e595118724b182c765ec6')

prepare() {
    mkdir -p "onlyoffice-${pkgver}/pkg"
    bsdtar -xf "${srcdir}/onlyoffice-desktopeditors-${CARCH}-${pkgver}.deb" -C "onlyoffice-${pkgver}"
    bsdtar -xf "onlyoffice-${pkgver}/data.tar.xz" -C "onlyoffice-${pkgver}/pkg"
}

package() {
    cp -dr --no-preserve='ownership' "onlyoffice-${pkgver}"/pkg/* "$pkgdir"

    install -d -m755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
    install -m644 "${srcdir}/onlyoffice.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/onlyoffice-desktopeditors.svg"
}
