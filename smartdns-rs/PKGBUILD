# Maintainer: Ven0m0
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=smartdns-rs
pkgver=0.13.0
pkgrel=1
pkgdesc='A cross-platform local DNS server written in Rust that returns the fastest IP address'
arch=('x86_64' 'aarch64')
url='https://github.com/mokeyish/smartdns-rs'
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('rust' 'cargo')
provides=('smartdns')
conflicts=('smartdns')
options=('strip' '!emptydirs' '!debug')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('4ffd4021d751dcdd572df9dffd2f2b554c12192baa9cdb93a604bace53644bb4')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=' -ffat-lto-objects'
  export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target RUSTFLAGS="-Ctarget-cpu=native -Copt-level=3 -Clink-arg=-fuse-ld=lld"
  cargo build -r --frozen --no-default-features -F resolve-cli,dns-over-tls,dns-over-quic,mdns,dnssec,service,nft,nom-recipes-all,swagger-ui-cdn,http_client,serde
}

check() {
  cd "$pkgname-$pkgver"
  RUSTUP_TOOLCHAIN=stable cargo test --frozen -r
}
package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/smartdns"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim: ts=2 sw=2 et:
