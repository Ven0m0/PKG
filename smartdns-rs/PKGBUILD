# Maintainer: Ven0m0
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=smartdns-rs
pkgver=0.13.0
pkgrel=1
pkgdesc='A cross-platform local DNS server written in Rust that returns the fastest IP address'
arch=('x86_64' 'aarch64')
url='https://github.com/mokeyish/smartdns-rs'
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('rust' 'cargo' 'clang' 'llvm' 'llvm-libs')
provides=('smartdns')
conflicts=('smartdns')
options=('strip' '!emptydirs' '!debug' '!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 CARGO_PROFILE_RELEASE_DEBUG=0

prepare() {
  cd "$pkgname-$pkgver"
  git clean -dfx
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=' -ffat-lto-objects'
  export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target RUSTFLAGS="-Ctarget-cpu=native -Copt-level=3 -Clink-arg=-fuse-ld=lld" CFLAGS
  cargo build -r --frozen --no-default-features -F resolve-cli,dns-over-tls,dns-over-quic,mdns,dnssec,service,nft,nom-recipes-all,swagger-ui-cdn,http_client,serde
}
check() {
  cd "$pkgname-$pkgver"
  RUSTUP_TOOLCHAIN=stable cargo test -r --frozen
}
package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/smartdns"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim: ts=2 sw=2 et:
