# Maintainer: Ven0m0
# Based on: https://archlinux.org/packages/extra/x86_64/ffmpeg/
# Based on: https://github.com/jellyfin/jellyfin-ffmpeg
# Optimizations from: https://github.com/Ven0m0/FFmpeg-Builds

_pkgname=ffmpeg
pkgname=ffmpeg-opt
pkgver=8.0.1
pkgrel=2
pkgdesc='Complete solution to record, convert and stream audio and video - debloated optimized build'
arch=(x86_64)
url='https://ffmpeg.org'
license=('GPL-3.0-only')
depends=(
  alsa-lib aribb24 bzip2 cairo expat fontconfig
  freetype2 fribidi glib2 glibc gmp gnutls
  harfbuzz jack lame libass libavc1394 libbluray libbs2b
  libdrm libdvdnav libdvdread libgl libiec61883 libjpeg-turbo
  libmodplug libopenmpt libpng libpulse libraw1394
  libsoxr libssh libva libwebp libx11 libxcb libxext
  libxv onevpl opus
  sdl2 snappy speex v4l-utils xvidcore
  xz zimg zlib
  dav1d
  libtheora libvorbis libvpx libxml2 rubberband vid.stab vmaf
  x264
)
makedepends=(
  avisynthplus clang lld nasm git ladspa cmake
  libgl mesa opencl-headers vulkan-headers ffnvcodec-headers
  lv2 gmp patch
)
optdepends=(
  'avisynthplus: AviSynthPlus support'
  'intel-media-sdk: Intel QuickSync support (legacy)'
  'onevpl-intel-gpu: for Intel Quick Sync Video (Alder Lake and newer)'
  'ladspa: LADSPA filters'
  'nvidia-utils: Nvidia NVDEC/NVENC support'
  'vulkan-intel: for Intel ANV Vulkan support'
)
provides=("ffmpeg=$pkgver" 'libavcodec.so' 'libavdevice.so' 'libavfilter.so'
  'libavformat.so' 'libavutil.so' 'libswresample.so' 'libswscale.so')
conflicts=('ffmpeg')
options=('strip')
source=(
  "https://ffmpeg.org/releases/ffmpeg-${pkgver}.tar.xz"
)
sha256sums=('05ee0b03119b45c0bdb4df654b96802e909e0a752f72e4fe3794f487229e5a41')

prepare() {
  cd "$_pkgname-$pkgver"
  # Apply patches if any
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src == *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 -i "../${src}" || git apply "../${src}"
  done
}

build() {
  cd "$_pkgname-$pkgver"
  # Use clang/lld for better optimization and faster linking
  export CC=clang CXX=clang++ LD=lld
  # Use -Oz for size optimization (debloat)
  export CFLAGS="${CFLAGS} -Oz"
  export CXXFLAGS="${CXXFLAGS} -Oz"
  ./configure --prefix=/usr --disable-debug --disable-static --disable-stripping --enable-shared --enable-gpl --enable-version3 --enable-nonfree --enable-lto \
    --enable-fontconfig --enable-gmp --enable-gnutls --enable-ladspa --enable-libaribb24 --enable-libass --enable-libbluray \
    --enable-libbs2b --enable-libdav1d --enable-libdrm --enable-libdvdnav --enable-libdvdread --enable-libfreetype --enable-libfribidi \
    --enable-libiec61883 --enable-libjack --enable-libmodplug --enable-libmp3lame \
    --enable-libopenmpt --enable-libopus --enable-libpulse \
    --enable-librubberband --enable-libsnappy --enable-libsoxr --enable-libspeex --enable-libssh --enable-libtheora \
    --enable-libv4l2 --enable-libvidstab --enable-libvmaf --enable-libvorbis --enable-libvpl --enable-libvpx --enable-libwebp --enable-libx264 \
    --enable-libxcb --enable-libxml2 --enable-libxvid --enable-libzimg --enable-nvdec --enable-nvenc --enable-opengl --enable-small \
    --enable-vaapi --disable-vdpau --enable-vulkan
  make -j"$(nproc)"
  make tools/qt-faststart
  rm -fv ./*-docs-*.pkg.tar.* ./*-debug-*.pkg.tar.*
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim: ts=2 sw=2 et:
