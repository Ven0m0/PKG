# Maintainer: Ven0m0
pkgname=update-alternatives-git
pkgver=r102.10fd49b
pkgrel=2
pkgdesc='Simple update-alternatives replacement written in Rust'
arch=('x86_64' 'aarch64')
url='https://github.com/fthomys/update-alternatives'
license=('MIT')
depends=('gcc-libs' 'glibc' 'zenity')
makedepends=('git' 'rust' 'cargo')
provides=("update-alternatives=${pkgver}")
conflicts=('update-alternatives')
options=('strip' '!emptydirs' '!debug')
source=("git+$url.git")
sha256sums=('SKIP')

export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 CARGO_PROFILE_RELEASE_DEBUG=0 CARGO_INCREMENTAL=0
export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target
command -v sccache &>/dev/null && export RUSTC_WRAPPER=sccache SCCACHE_IDLE_TIMEOUT=10800 SCCACHE_DIRECT=true

pkgver(){
  cd update-alternatives
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build(){
  cd update-alternatives
  export RUSTFLAGS="-Ctarget-cpu=native -Copt-level=3 -Ccodegen-units=1 -Clto=fat -Clink-arg=-fuse-ld=lld -Cstrip=symbols -Cpanic=abort" CFLAGS+=' -ffat-lto-objects'
  cargo build -r --locked
}

check(){
  cd update-alternatives
  cargo test -r --locked
}

package(){
  cd update-alternatives
  install -Dm755 "target/release/update-alternatives" "$pkgdir/usr/bin/update-alternatives"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 man/update-alternatives.1 "$pkgdir/usr/share/man/man1/update-alternatives.1"
  install -Dm644 data/update-alternatives.desktop "$pkgdir/usr/share/applications/update-alternatives.desktop"
  install -Dm644 /dev/stdin "$pkgdir/usr/share/libalpm/hooks/update-alternatives.hook" <<'EOF'
[Trigger]
Operation = Install
Operation = Upgrade
Operation = Remove
Type = Path
Target = usr/bin/*
Target = usr/local/bin/*
Target = etc/alternatives/*

[Action]
Description = Rebuilding alternatives symlinks
When = PostTransaction
Exec = /usr/bin/update-alternatives --sync
EOF
}
