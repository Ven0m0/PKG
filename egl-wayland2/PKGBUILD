# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland2
pkgver=1.0.1
pkgrel=1
_commit=d71abc0f87f2694a446aba5b5cd919d8a2704de3
pkgdesc="new EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland2"
arch=(x86_64)
license=(MIT)
depends=(
  eglexternalplatform
  glibc
  libdrm
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland2.so)
source=("git+https://github.com/NVIDIA/egl-wayland2#commit=$_commit")
b2sums=('2185e7c1e7e884593583eaa3641b899feb5a5c07628ead6d3f738097b246546cb6cb79868ca04a8ec49d38b862a3bd8e55cbb291c7f46873faf668a9cea80383')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
