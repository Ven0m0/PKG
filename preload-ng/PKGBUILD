# Maintainer: Ven0m0
pkgname=preload-ng
pkgver=0.6.7
_commit=5a637c13652916666ca7d8214403ca22a55e4d48
pkgrel=1
pkgdesc='Adaptive readahead daemon that prefetches files to reduce application startup times'
arch=('x86_64')
url='https://github.com/miguel-b-p/preload-ng'
license=('GPL-2.0-only')
depends=('glib2')
makedepends=('git')
backup=('etc/preload.conf')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${_commit}.tar.gz")
sha256sums=('663199e22197e5c3baf5c6fa2e734eaaeca16b2eb50efec578cf5320a6d32a5c')

build() {
  cd "${pkgname}-${_commit}/preload-src"
  make -j"$(nproc)"
}

package() {
  cd "${pkgname}-${_commit}/preload-src"
  install -Dm755 preload "${pkgdir}/usr/sbin/preload"
  install -Dm644 preload.conf "${pkgdir}/etc/preload.conf"
  # Install systemd service
  install -Dm644 /dev/stdin "${pkgdir}/usr/lib/systemd/system/${pkgname}.service" <<'EOF'
[Unit]
Description=Preload-NG Adaptive Readahead Daemon
Documentation=https://github.com/miguel-b-p/preload-ng
After=local-fs.target

[Service]
Type=simple
ExecStart=/usr/sbin/preload --foreground --logfile ""
ExecReload=/bin/kill -HUP $MAINPID
Restart=on-failure
RestartSec=5
ProtectSystem=full
ProtectHome=true
PrivateTmp=true
NoNewPrivileges=true
StateDirectory=preload
ReadWritePaths=/var/log

[Install]
WantedBy=multi-user.target
EOF
}
# vim: ts=2 sw=2 et:
