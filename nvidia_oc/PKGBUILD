pkgname=nvidia_oc
pkgver=0.1.21
pkgrel=1
makedepends=('rust' 'cargo')
depends=('nvidia-utils' 'gcc-libs')
arch=('x86_64' 'aarch64')
pkgdesc="A simple command line tool to overclock Nvidia GPUs using the NVML library on Linux. This supports both X11 and Wayland."
url='https://github.com/Dreaming-Codes/nvidia_oc'
license=('MIT')
options=('strip' '!emptydirs' '!debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Dreaming-Codes/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('704bc12b0854c0558803290c6e9fa391a31dd62fcd642c1bbf7dd5cb8face555')
# Generated in accordance to https://wiki.archlinux.org/title/Rust_package_guidelines.
# Might require further modification depending on the package involved.
prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=' -ffat-lto-objects'
  export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target RUSTFLAGS="-C target-cpu=native -C opt-level=3 -C link-arg=-fuse-ld=lld"
  cargo build --frozen -r --all-features
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -Dm0644 /dev/stdin "$pkgdir/usr/lib/systemd/system/nvidia_oc.service" <<'EOF'
[Unit]
Description=NVIDIA Overclocking Service
After=network.target

[Service]
ExecStart=/usr/bin/nvidia_oc set --index 0 --power-limit 200000 --freq-offset 160 --mem-offset 750 --min-clock 0 --max-clock 2000
User=root
Group=root
LimitNOFILE=65536
Restart=on-failure

[Install]
WantedBy=multi-user.target
EOF
}

# vim: ts=2 sw=2 et:
