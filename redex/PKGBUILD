# Maintainer: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>
pkgname=redex-git
_pkgname=redex
pkgver=r681.44b8fd3
pkgrel=1
pkgdesc="A bytecode optimizer for Android apps"
arch=('i686' 'x86_64')
url="http://fbredex.com/"
license=('BSD')
depends=('python' 'boost-libs' 'jsoncpp')
makedepends=('git' 'boost')
provides=('redex')
conflicts=('redex')
options=('strip')
source=("git+https://github.com/facebook/${_pkgname}/")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgname}"
  sed -i 's/ANDROID_SDK/ANDROID_HOME/g' redex.py
}

build() {
  cd "${_pkgname}"
  export CFLAGS="${CFLAGS} -O3 -march=native -mtune=native -pipe -fno-plt -flto=auto"
  export CXXFLAGS="${CXXFLAGS} -O3 -march=native -mtune=native -pipe -fno-plt -flto=auto"
  export LDFLAGS="${LDFLAGS} -Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"
  autoreconf -ivf
  ./configure --prefix=/usr
  make -j"$(nproc)"
}

package() {
  cd "${_pkgname}"
  make DESTDIR="$pkgdir/" install
}
