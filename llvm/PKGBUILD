# Maintainer: Ven0m0
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Lone_Wolf <lone_wolf@klaas-de-kat.nl>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: blackmoon3 <https://github.com/blacksky3>
# Contributor: yurikoles <root@yurikoles.com>
# Contributor: bearoso <bearoso@gmail.com>
# Contributor: Luchesar V. ILIEV <luchesar%2eiliev%40gmail%2ecom>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Hesiod (https://github.com/hesiod)
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

pkgbase=llvm-minimal
pkgname=(llvm-minimal llvm-libs-minimal clang-minimal clang-libs-minimal clang-opencl-headers-minimal)
pkgver=21.0.0_r537041.f2e62cfca5e5
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('Apache-2.0 WITH LLVM-exception')
makedepends=(git cmake ninja libffi libedit ncurses libxml2 libxcrypt
  python-setuptools python-build python-installer python-wheel python-psutil zlib zstd)
options=('staticlibs' '!lto' '!debug') # tools/llvm-shlib/typeids.test fails with LTO
source=("git+https://github.com/llvm/llvm-project.git")
sha512sums=('SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'D574BD5D1D0E98895E3BF90044F2485E45D59042'  # Tobias Hieta <tobias@hieta.se>
              'FFB3368980F3E6BB5737145A316C56D064CACBA5'  # Douglas Yung <douglas.yung@sony.com>
              '71046D1E9C6656BDD61171873E83BABF4A4F9E85'  # Cullen Rhodes <cullen.rhodes@arm.com>
)

# Ninja & LIT by default use all available cores. This can lead to heavy stress on systems making them unresponsive.
# It can also cause the kernel oom killer to interfere and kill important tasks.
# A reasonable value to avoid these issues appears to be 75% of available cores.
# This PKGBUILD uses the env vars NINJAFLAGS & LITFLAGS to achieve this. They should be set on command line or in files read by your shell on login (like .bashrc).
# Example for systems with 24 cores:
#   export NINJAFLAGS="-j 18 -l 18"
#   export LITFLAGS="-j 18"
# NOTE: It's your responsibility to validate the value of LITFLAGS & NINJAFLAGS. If unsure, don't set them.

# Utilizing LLVM_DISTRIBUTION_COMPONENTS to avoid
# installing static libraries; inspired by Gentoo
_get_distribution_components() {
  local target include
  ninja -t targets | grep -Po 'install-\K.*(?=-stripped:)' | while read -r target; do
    case $target in
      llvm-libraries|clang-libraries|clang-tidy-headers|distribution) include=0 ;;
      LLVM) include=1 ;;
      LLVMgold) include=0 ;;
      # libraries needed for clang-tblgen
      LLVMDemangle|LLVMSupport|LLVMTableGen) include=1 ;;
      # used by lldb
      LLVMDebuginfod) include=1 ;;
      # testing libraries
      LLVMTestingAnnotations|LLVMTestingSupport) include=1 ;;
      # exclude static libraries
      LLVM*) include=0 ;;
      # exclude llvm-exegesis (doesn't seem useful without libpfm)
      llvm-exegesis) include=0 ;;
      clang|clangd|clang-*) include=1 ;;
      clang*|findAllSymbols) include=0 ;;
      *) include=1 ;;
    esac
    [[ $include -eq 1 ]] && echo "$target"
  done
}

pkgver() {
  cd llvm-project/cmake/Modules

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
    'BEGIN { ORS="." ; i=0 } \
     /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
     END { print "\n" }' \
    LLVMVersion.cmake)_r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

build() {
  # Build only minimal debug info to reduce size
  CFLAGS=${CFLAGS/-g /-g0 }
  CXXFLAGS=${CXXFLAGS/-g /-g0 }

  # Add fno-semantic-interposition, can improve performance at fPIC compiled packages massively
  # At clang it improves for 3%
  export CFLAGS+=" -fno-semantic-interposition"
  export CXXFLAGS+=" -fno-semantic-interposition"

  local cmake_args=(
    -G Ninja
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_DOCDIR=share/doc
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_SKIP_RPATH=ON
    -D LLVM_BINUTILS_INCDIR=/usr/include
    -D FFI_INCLUDE_DIR=$(pkg-config --variable=includedir libffi)
    -D LLVM_VERSION_SUFFIX=""
    -D LLVM_APPEND_VC_REV=ON
    -D LLVM_HOST_TRIPLE=$CHOST
    -D LLVM_TARGETS_TO_BUILD="X86"
    -D LLVM_BUILD_LLVM_DYLIB=ON
    -D LLVM_LINK_LLVM_DYLIB=ON
    -D CLANG_LINK_CLANG_DYLIB=ON
    -D LLVM_DYLIB_COMPONENTS="all"
    -D LLVM_INSTALL_UTILS=ON
    -D LLVM_ENABLE_RTTI=ON
    -D LLVM_ENABLE_FFI=ON
    -D LLVM_USE_PERF=ON
    -D LLVM_INCLUDE_BENCHMARKS=OFF
    -D LLVM_INCLUDE_EXAMPLES=OFF
    -D LLVM_BUILD_DOCS=OFF
    -D LLVM_INCLUDE_DOCS=OFF
    -D LLVM_ENABLE_OCAMLDOC=OFF
    -D LLVM_ENABLE_SPHINX=OFF
    -D LLVM_ENABLE_DOXYGEN=OFF
    -D LLVM_ENABLE_BINDINGS=OFF
    -D LLVM_ENABLE_RUNTIMES=compiler-rt
    -D LLVM_ENABLE_PROJECTS="clang;clang-tools-extra"
    -D LLVM_ENABLE_DUMP=ON
    -D LLVM_ENABLE_ASSERTIONS=OFF
    -D LLVM_ENABLE_CURL=ON
    -D LLVM_BUILD_TESTS=ON
    -D LLVM_INSTALL_GTEST=OFF
    -D SPHINX_WARNINGS_AS_ERRORS=OFF
    -D PACKAGE_BUGREPORT=https://github.com/Ven0m0/PKG/issues
    -D LLVM_LIT_ARGS="$LITFLAGS"" -sv --ignore-fail"
  )

  cmake -B _build -S "$srcdir"/llvm-project/llvm "${cmake_args[@]}" -Wno-dev

  pushd "$srcdir"/_build
  local distribution_components=$(_get_distribution_components | paste -sd\;)
  popd
  test -n "$distribution_components"
  cmake_args+=(-D LLVM_DISTRIBUTION_COMPONENTS="$distribution_components")

  cmake -B _build -S "$srcdir"/llvm-project/llvm "${cmake_args[@]}" -Wno-dev

  ninja $NINJAFLAGS -C _build

  # Include lit for running lit-based tests in other projects
  pushd llvm-project/llvm/utils/lit
  python -m build --wheel --no-isolation
  popd
}

check() {
  ninja $NINJAFLAGS -C _build check-llvm
  ninja $NINJAFLAGS -C _build check-clang
  ninja $NINJAFLAGS -C _build check-clang-tools
}

package_llvm-minimal() {
  pkgdesc="Collection of modular and reusable compiler and toolchain technologies (optimized)"
  depends=(llvm-libs-minimal="$pkgver-$pkgrel" zstd zlib glibc gcc-libs curl perl)
  provides=('llvm')
  conflicts=('llvm')
  optdepends=('python: for using lit (LLVM Integrated Tester)'
              'python-setuptools: for using lit')

  DESTDIR="$pkgdir" ninja $NINJAFLAGS -C _build install-distribution

  # Include lit for running lit-based tests in other projects
  pushd "$srcdir"/llvm-project/llvm/utils/lit
  python3 setup.py install --root="$pkgdir" -O1
  popd

  # Remove some symlinks to prevent a conflict with repo llvm-libs
  rm "$pkgdir"/usr/lib/lib{LLVM,LTO}.so
  rm "$pkgdir"/usr/lib/libRemarks.so

  # Some static libraries for clang are still built and !staticlibs is unable to remove them
  # force their removal
  find "$pkgdir"/usr/lib/clang -depth -type f -name "*.a" -delete

  # clang uses clang/LLVM_Major_version in its folder structure
  # extract this from pkgver
  local _major_ver=$(echo $pkgver | cut -d. -f1)

  # prepare folders in srcdir to store files that are placed in other package_*() functions
  mkdir -p "$srcdir"/llvm-libs/usr/lib
  mkdir -p "$srcdir"/clang/usr/{bin,include,lib,lib/cmake,share}
  mkdir -p "$srcdir"/clang-libs/usr/lib
  mkdir -p "$srcdir"/clang-opencl-headers/usr/{lib/clang/$_major_ver/include,include/clang/Basic}

  # The llvm runtime libraries go into llvm-libs-minimal
  mv -f "$pkgdir"/usr/lib/{libLLVM*.so*,libLTO.so.*,libRemarks.so.*} "$srcdir"/llvm-libs/usr/lib

  # The clang runtime libraries go into clang-libs-minimal
  mv -f "$pkgdir"/usr/lib/libclang{,-cpp}.so* "$srcdir"/clang-libs/usr/lib

  # clang opencl files go to clang-opencl-headers-minimal
  mv -f "$pkgdir"/usr/lib/clang/$_major_ver/include/opencl* "$srcdir"/clang-opencl-headers/usr/lib/clang/$_major_ver/include
  mv -f "$pkgdir"/usr/include/clang/Basic/OpenCL* "$srcdir"/clang-opencl-headers/usr/include/clang/Basic

  # clang-minimal files go to a separate package
  mv -f "$pkgdir"/usr/lib/{libear,libscanbuild,clang} "$srcdir"/clang/usr/lib
  mv -f "$pkgdir"/usr/lib/cmake/clang "$srcdir"/clang/usr/lib/cmake/
  mv -f "$pkgdir"/usr/include/{clang,clang-c} "$srcdir"/clang/usr/include/

  # llvm project uses /usr/libexec and setting CMAKE_INSTALL_LIBEXECDIR doesn't affect that.
  # to comply with archlinux packaging standards we have to move some files manually
  if [[ -d "$pkgdir"/usr/libexec ]]; then
    mv -f "$pkgdir"/usr/libexec/* "$srcdir"/clang/usr/lib/clang
    rmdir "$pkgdir"/usr/libexec
  fi

  mv -f "$pkgdir"/usr/bin/{analyze-build,c-index-test,clang*,diagtool,find-all-symbols,git-clang-format,hmaptool,intercept-build,modularize,pp-trace,run-clang-tidy,scan-build,scan-build-py,scan-view} "$srcdir"/clang/usr/bin/ 2>/dev/null || true
  mv -f "$pkgdir"/usr/share/{clang,clang-doc,man,opt-viewer,scan-build,scan-view} "$srcdir"/clang/usr/share/ 2>/dev/null || true

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE.TXT
}

_python_optimize() {
  python -m compileall "$@"
  python -O -m compileall "$@"
  python -OO -m compileall "$@"
}

package_llvm-libs-minimal() {
  pkgdesc="LLVM runtime libraries (optimized)"
  depends=(gcc-libs zlib zstd libffi libedit libxml2 ncurses glibc)
  provides=('libLLVM.so' 'libLTO.so' 'libRemarks.so' 'llvm-libs')
  conflicts=('llvm-libs')

  cp --preserve --recursive "$srcdir"/llvm-libs/* "$pkgdir"/

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.TXT
}

package_clang-minimal() {
  pkgdesc='C language family frontend for LLVM (optimized)'
  depends=(llvm-libs-minimal="$pkgver-$pkgrel" clang-libs-minimal="$pkgver-$pkgrel" gcc-libs glibc)
  optdepends=('openmp: OpenMP support in clang with -fopenmp'
              'python: for scan-view, scan-build, git-clang-format, clang-rename and python bindings'
              'llvm-minimal: referenced by some clang headers')
  conflicts=(compiler-rt clang)
  provides=(compiler-rt clang clang-analyzer clang-tools-extra)

  cp --preserve --recursive "$srcdir"/clang/* "$pkgdir"/

  # Move scanbuild-py into site-packages
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/$site_packages"
  mv "$pkgdir"/usr/lib/{libear,libscanbuild} "$pkgdir/$site_packages/" 2>/dev/null || true

  sed -i 's|libexec|lib/clang|' \
    "$pkgdir/usr/bin/scan-build" \
    "$pkgdir/$site_packages/libscanbuild/analyze.py" 2>/dev/null || true

  # Compile Python scripts
  _python_optimize "$pkgdir"/usr/share "$pkgdir"/$site_packages

  install -Dm644 "$srcdir"/llvm-project/compiler-rt/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/compiler-rt-LICENSE.TXT
  install -Dm644 "$srcdir"/llvm-project/clang-tools-extra/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/clang-tools-extra-LICENSE.TXT
  install -Dm644 "$srcdir"/llvm-project/clang/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/clang-LICENSE.TXT
}

package_clang-libs-minimal() {
  pkgdesc="clang runtime libraries (optimized)"
  depends=(llvm-libs-minimal="$pkgver-$pkgrel" gcc-libs glibc)
  provides=('clang-libs')
  conflicts=("clang<$pkgver-$pkgrel" 'clang-libs')

  cp --preserve --recursive "$srcdir"/clang-libs/* "$pkgdir"/

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.TXT
}

package_clang-opencl-headers-minimal() {
  pkgdesc="clang headers & include files for OpenCL (optimized)"
  depends=(clang-libs-minimal="$pkgver-$pkgrel")
  arch=('any')
  provides=('clang-opencl-headers')
  conflicts=("clang<$pkgver-$pkgrel" 'clang-opencl-headers')

  cp --preserve --recursive "$srcdir"/clang-opencl-headers/* "$pkgdir"/

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.TXT
}

# vim: set ft=sh ts=2 sw=2 et:
