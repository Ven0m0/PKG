# Maintainer: Ven0m0
_pkgname=aria2
pkgname="$_pkgname-ven0m0"
pkgver=1.37.0
pkgrel=2
pkgdesc='Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink'
arch=('x86_64')
url='https://aria2.github.io'
license=('GPL')
depends=('openssl' 'libxml2' 'sqlite' 'c-ares' 'ca-certificates' 'libssh2')
makedepends=('patch')
checkdepends=('cppunit')
provides=('aria2')
conflicts=('aria2')
options=('strip')
source=("https://github.com/aria2/aria2/releases/download/release-${pkgver}/aria2-${pkgver}.tar.xz"
        '0001-tweak-aria2-for-speed.patch'
        '0002-retry-options.patch')
sha256sums=('60a420ad7085eb616cb6e2bdf0a7206d68ff3d37fb5a956dc44242eb2f79b66b'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src == *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 -i "$srcdir/$src"
  done
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-libaria2 \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --without-gnutls \
    --with-openssl

  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Add bash completion
  install -d "$pkgdir"/usr/share/bash-completion/completions
  install -m644 "$pkgdir"/usr/share/doc/aria2/bash_completion/aria2c \
    "$pkgdir"/usr/share/bash-completion/completions
  rm -rf "$pkgdir"/usr/share/doc/aria2/bash_completion
}

# vim: ts=2 sw=2 et:
