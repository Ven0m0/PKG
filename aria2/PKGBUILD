# Maintainer: Ven0m0
_pkgname=aria2
pkgname="$_pkgname-ven0m0"
pkgver=1.37.0
pkgrel=3
pkgdesc='Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink'
arch=('x86_64')
url='https://aria2.github.io'
license=('GPL-2.0-or-later')
depends=('openssl' 'libxml2' 'sqlite' 'c-ares' 'ca-certificates' 'libssh2')
checkdepends=('cppunit')
provides=('aria2')
conflicts=('aria2')
options=('strip')
source=("https://github.com/aria2/aria2/releases/download/release-${pkgver}/aria2-${pkgver}.tar.xz"
  '0001-tweak-aria2-for-speed.patch'
  '0002-retry-options.patch')
sha256sums=('60a420ad7085eb616cb6e2bdf0a7206d68ff3d37fb5a956dc44242eb2f79b66b'
  '0a83b33c3b4b14ba622fb039d4183e2cbb578eb97cc9cf4dbbb3ba32c28f5354'
  'e6127b3eb368eb8810a34087fa29a79a201af62531e3a6f62614d8638055bb3c')

prepare() {
  cd "$_pkgname-$pkgver"
  patch -Np1 -i ../0001-tweak-aria2-for-speed.patch
  patch -Np1 -i ../0002-retry-options.patch
}

build() {
  cd "$_pkgname-$pkgver"
  export CFLAGS="${CFLAGS/-O2/-O3} -pipe -fno-plt -fstack-protector-strong"
  export CXXFLAGS="${CXXFLAGS/-O2/-O3} -pipe -fno-plt -fstack-protector-strong"
  export LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"
  ./configure --prefix=/usr --enable-libaria2 --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt --without-gnutls --with-openssl
  make
}

check() {
  cd "$_pkgname-$pkgver"
  make check
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$pkgdir"/usr/share/doc/aria2/bash_completion/aria2c "$pkgdir"/usr/share/bash-completion/completions/aria2c
  rm -rf "$pkgdir"/usr/share/doc/aria2/bash_completion
}

# vim: ts=2 sw=2 et:
