# Maintainer: Josh Stoik <josh@blobfolio.com>
pkgname=flaca
pkgver=3.5.2
pkgrel=1
makedepends=('rust' 'cargo')
arch=('x86_64' 'aarch64')
url='https://github.com/Blobfolio/flaca'
pkgdesc="Brute-force, lossless GIF, JPEG, and PNG compression."
license=('WTFPL')
options=('strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Blobfolio/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target
  export RUSTFLAGS="-C target-cpu=native -C opt-level=3 -C link-arg=-fuse-ld=lld"
  cargo build --frozen -r --all-features --bins --lib
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}

# vim: ts=2 sw=2 et:
