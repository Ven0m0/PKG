# shellcheck enable=all shell=bash source-path=SCRIPTDIR
# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=heroic-games-launcher-electron-git
_pkgname=HeroicGamesLauncher
pkgver=2.18.1.r20.g581386ca
pkgrel=1
pkgdesc="Native GOG, Epic Games and Amazon games launcher for Linux, with the system electron."
arch=(x86_64)
url="https://heroicgameslauncher.com/"
license=(GPL-3.0-only)
_electron=electron36
depends=($_electron zlib gcc-libs glibc)
makedepends=(git npm python desktop-file-utils)
optdepends=('bun: build with bun' 'pnpm: build with pnpm')
provides=(heroic-games-launcher)
conflicts=(heroic-games-launcher)
source=(git+https://github.com/Heroic-Games-Launcher/HeroicGamesLauncher.git)
sha256sums=('SKIP')
options=('strip' '!debug')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare() {
  cd "$_pkgname"
  sed -i -e "s/Exec=heroic-run /Exec=heroic /" "flatpak/com.heroicgameslauncher.hgl.desktop"
}
build() {
  cd "$_pkgname"
  local _electron_version=$(< /usr/lib/$_electron/version)
  local -a _builder_opts=(-c.electronDist="/usr/lib/${_electron}/" -c.electronVersion="${_electron_version}")
  if command -v bun &>/dev/null; then
    NODE_ENV=development bun install
    NODE_ENV=production bun run download-helper-binaries
    NODE_ENV=production bun run dist:linux --x64 --dir "${_builder_opts[@]}"
  elif command -v pnpm &>/dev/null; then
    NODE_ENV=development pnpm install
    NODE_ENV=production pnpm run download-helper-binaries
    NODE_ENV=production pnpm run dist:linux --x64 --dir "${_builder_opts[@]}"
  else
    NODE_ENV=development npm install
    NODE_ENV=production npm run download-helper-binaries
    NODE_ENV=production npm run dist:linux -- --x64 --dir "${_builder_opts[@]}"
  fi
}
package() {
  install -d "${pkgdir}/usr/lib/heroic"
  install -d "${pkgdir}/usr/bin"
  # removing arm64 binaries
  rm -rf ./$_pkgname/dist/linux-unpacked/resources/app.asar.unpacked/build/bin/arm64/
  # copying libs
  cp -R ./$_pkgname/dist/linux-unpacked/. "${pkgdir}/usr/lib/heroic/"
  # executable
  ln -sf "/usr/lib/heroic/heroic" "${pkgdir}/usr/bin/heroic"
  # icon
  install -Dm644 ./$_pkgname/flatpak/com.heroicgameslauncher.hgl.png -t "${pkgdir}/usr/share/pixmaps"
  # metainfo
  install -Dm644 ./$_pkgname/flatpak/templates/com.heroicgameslauncher.hgl.metainfo.xml.template "${pkgdir}"/usr/share/metainfo/com.heroicgameslauncher.hgl.metainfo.xml
  # fix icon on Gnome dock
  desktop-file-edit --set-key=Exec --set-value="heroic %U" --set-key=StartupWMClass --set-value=heroic flatpak/com.heroicgameslauncher.hgl.desktop
  # desktop file
  install -Dm644 ./$_pkgname/flatpak/com.heroicgameslauncher.hgl.desktop -t "${pkgdir}/usr/share/applications"
}
# vim:set sw=2 ts=2 et:
