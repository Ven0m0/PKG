# Maintainer: Ven0m0 <ven0m0.wastaken@gmail.com>
pkgname=filen-desktop
pkgver=3.0.48
pkgrel=1
pkgdesc="Filen Desktop Client - syncing, virtual drive, S3, WebDAV, file browsing"
arch=('x86_64' 'aarch64')
url="https://filen.io"
license=('AGPL3')
depends=('electron' 'nodejs' 'fuse3')
makedepends=('bun' 'git' 'jq')
provides=('filen-desktop')
conflicts=('filen-desktop-bin')
source=("git+https://github.com/FilenCloudDienste/filen-desktop.git#commit=81950031c41831b7048669cc9cfba0c75070b478")
sha256sums=('SKIP')

build(){
  cd "$srcdir/$pkgname" || exit 1
  export NODE_ENV=production
  export BUN_INSTALL_CACHE_DIR="$srcdir/. bun-cache"
  bun install --frozen-lockfile --no-save
  case "$CARCH" in
    x86_64)
      bun add --force \
        @parcel/watcher-linux-x64-glibc@latest \
        @parcel/watcher-linux-x64-musl@latest
      ;;
    aarch64)
      bun add --force \
        @parcel/watcher-linux-arm64-glibc@latest \
        @parcel/watcher-linux-arm64-musl@latest
      ;;
  esac
  bun run build
}

package(){
  cd "$srcdir/$pkgname" || exit 1
  install -dm755 "$pkgdir/opt/$pkgname"
  cp -r dist node_modules assets public package.json "$pkgdir/opt/$pkgname/"
  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$pkgname" <<'EOF'
#!/usr/bin/env bash
exec electron /opt/filen-desktop/dist/index.js "$@"
EOF
  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$pkgname.desktop" <<EOF
[Desktop Entry]
Type=Application
Name=Filen
Comment=Zero-knowledge encrypted cloud storage
Exec=filen-desktop %U
Icon=filen-desktop
Categories=Network;FileTransfer;
Terminal=false
StartupWMClass=Filen
MimeType=x-scheme-handler/filen;
EOF
  for size in 16 32 48 64 128 256 512; do
    if [[ -f "build/icons/png/${size}x${size}.png" ]]; then
      install -Dm644 "build/icons/png/${size}x${size}. png" \
        "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/$pkgname.png"
    fi
  done 
  [[ -f LICENSE.md ]] && install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
