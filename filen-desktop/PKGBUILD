# Maintainer:  Ven0m0 <ven0m0.wastaken@gmail.com>
# Contributor: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=filen-desktop-git
_pkgname="Filen Desktop"
pkgver=3.0.47.r0.ge2ec0fd
_electronversion=34
_nodeversion=20
pkgrel=1
pkgdesc="Desktop client including Syncing, Virtual Drive mounting, S3, WebDAV, File Browsing, Chats, Notes, Contacts and more.  Use system-wide electron."
arch=('any')
url="https://filen.io/"
_ghurl="https://github.com/FilenCloudDienste/filen-desktop"
license=('AGPL-3.0-only')
conflicts=("${pkgname%-git}")
provides=("${pkgname%-git}=${pkgver%.r*}")
depends=(
  "electron${_electronversion}"
  'libjpeg6-turbo'
  'giflib'
)
makedepends=(
  'gendesk'
  'bun'
  'fnm'
  'git'
  'curl'
)
source=(
  "${pkgname%-git}.git::git+${_ghurl}"
  "${pkgname%-git}.sh"
)
sha256sums=('SKIP'
            '291f50480f5a61bc9c68db7d44cd0412071128706baa868a9cb854f8779a1980')

pkgver(){
  cd "${srcdir}/${pkgname%-git}.git"
  set -o pipefail
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

_ensure_local_fnm(){
  export FNM_DIR="${srcdir}/.fnm"
  export PATH="${FNM_DIR}/aliases/default/bin:${PATH}"
  eval "$(fnm env --use-on-cd)"
  fnm install "${_nodeversion}"
  fnm use "${_nodeversion}"
}

prepare(){
  sed -e "
    s/@electronversion@/${_electronversion}/g
    s/@appname@/${pkgname%-git}/g
    s/@runname@/app.asar/g
    s/@cfgdirname@/${_pkgname}/g
    s/@options@/env ELECTRON_OZONE_PLATFORM_HINT=auto/g
  " -i "${srcdir}/${pkgname%-git}.sh"
  _ensure_local_fnm
  gendesk -q -f -n --pkgname="${pkgname%-git}" --pkgdesc="${pkgdesc}" --categories="Utility" --name="${pkgname%-git}" --exec="${pkgname%-git} %U"
  cd "${srcdir}/${pkgname%-git}.git"
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  export SYSTEM_ELECTRON_VERSION="$(electron${_electronversion} -v | sed 's/v//g')"
  export electronDist="/usr/lib/electron${_electronversion}"
  export BUN_INSTALL_CACHE_DIR="${srcdir}/.bun-cache"
  HOME="${srcdir}/.electron-gyp"
  
  if [[ "$(curl -s ipinfo.io/country)" == *"CN"* ]]; then
    cat >bunfig.toml <<EOF
[install]
registry = "https://registry.npmmirror.com"
[install.cache]
dir = "${srcdir}/.bun-cache"
[install.lockfile]
save = true
EOF
  else
    cat >bunfig.toml <<EOF
[install.cache]
dir = "${srcdir}/.bun-cache"
[install.lockfile]
save = true
EOF
  fi
  sed -i "s/\"electron\": \"[^\"]*\"/\"electron\": \"${SYSTEM_ELECTRON_VERSION}\"/g" package.json
  NODE_ENV=development bun install --no-save
}

build(){
  cd "${srcdir}/${pkgname%-git}.git"
  _ensure_local_fnm
  NODE_ENV=production bun run build
  NODE_ENV=production bun x electron-builder --linux dir -c. electronDist="${electronDist}"
}

package(){
  install -Dm755 "${srcdir}/${pkgname%-git}.sh" "${pkgdir}/usr/bin/${pkgname%-git}"
  install -Dm644 "${srcdir}/${pkgname%-git}. git/prod/linux-"*/resources/app.asar -t "${pkgdir}/usr/lib/${pkgname%-git}"
  cp -Pr --no-preserve=ownership "${srcdir}/${pkgname%-git}.git/prod/linux-"*/resources/{app.asar.unpacked,public} "${pkgdir}/usr/lib/${pkgname%-git}"
  install -Dm644 "${srcdir}/${pkgname%-git}.desktop" -t "${pkgdir}/usr/share/applications"
  local icon_sizes=(16x16 24x24 32x32 48x48 64x64 128x128 256x256 512x512 1024x1024)
  for _icons in "${icon_sizes[@]}"; do
    install -Dm644 "${srcdir}/${pkgname%-git}.git/build/icons/png/${_icons}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_icons}/apps/${pkgname%-git}.png"
  done
  install -Dm644 "${srcdir}/${pkgname%-git}.git/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
