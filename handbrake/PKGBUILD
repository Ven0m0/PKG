# Maintainer: Holger Obermaier <holgerob@gmx.de>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=('handbrake-svt-av1-essential-llvm-optimized' 'handbrake-svt-av1-essential-llvm-optimized-cli')
pkgver=1.10.2.r4.g53aeefb9e
pkgrel=1
arch=('x86_64')
url="https://handbrake.fr/"
license=('GPL')
_commondeps=(
  'bzip2'
  'fribidi'
  'gcc-libs'
  'jansson'
  'lame'
  'libass'
  'libjpeg-turbo'
  'libogg'
  'libtheora'
  'libva'
  'libvorbis'
  'libvpx'
  'libxml2'
  'numactl'
  'opus'
  'speex'
  'x264'
  'xz'
  'zlib'
)
_guideps=(
  'at-spi2-core'
  'cairo'
  'fontconfig'
  'freetype2'
  'gdk-pixbuf2'
  'glib2'
  'gst-plugins-base'
  'gst-plugins-base-libs'
  'gstreamer'
  'gtk4'
  'harfbuzz'
  'libgudev'
  'pango'
  'desktop-file-utils' 'hicolor-icon-theme'
)
makedepends=(
  'base-devel'
  'intltool'
  'python'
  'nasm'
  'wget'
  'cmake'
  'meson'
  'git'
  'clang>=20'
  'lld'
  'llvm'
  'cargo-c'
  # AMD VCE encoding on Linux requires Vulkan
  #'vulkan-headers'
  "${_commondeps[@]}"
  "${_guideps[@]}"
)
_implicitdeps=(xz zlib glibc gcc-libs bzip2 libdrm)
optdepends=('intel-media-sdk: for enabling Intel QSV' 'amf-amdgpu-pro: for enabling AMD AMF'
  'nvidia-utils: for enabling Nvidia nvenc and nvdec' 'cuda: for enabling Nvidia nvenc and nvdec'
  'gst-plugins-good: for video previews' 'libdvdcss: for decoding encrypted DVDs' 'libdvdcss: for decoding encrypted DVDs')
options=('!lto' 'strip' '!debug') # https://bugs.archlinux.org/task/72600
source=("HandBrake::git+https://github.com/HandBrake/HandBrake.git#tag=${pkgver}")
sha256sums=('SKIP')
pkgver(){ git -C HandBrake/ gc --auto --prune=now; git -C HandBrake/ describe ${_commit:-} | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'; }

setup_compiler(){
  export CC="/usr/bin/clang" CXX="/usr/bin/clang++"; unset CFLAGS CXXFLAGS
  export CPP="/usr/bin/clang-cpp" LD="/usr/bin/lld" STRIP="/usr/bin/llvm-strip"
  export CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=3/-D_FORTIFY_SOURCE=2}" CXXFLAGS="${CXXFLAGS/-D_FORTIFY_SOURCE=3/-D_FORTIFY_SOURCE=2}"
  export LDFLAGS="-fuse-ld=lld"
  export AR="/usr/bin/llvm-ar" RANLIB="/usr/bin/llvm-ranlib" NM="/usr/bin/llvm-nm"
  export ADDR2LINE="/usr/bin/llvm-addr2line" READELF="/usr/bin/llvm-readelf"
  export OBJCOPY="/usr/bin/llvm-objcopy" OBJDUMP="/usr/bin/llvm-objdump"
}

build(){
  bash "${startdir}/patch.sh"
  setup_compiler
  local -a CONFIGURE_OPTIONS=(
    --launch-jobs=0 --prefix=/usr --harden
    --cc="${CC}" --ar="${AR}" --ranlib="${RANLIB}" --strip="${STRIP}"
    --lto=on --optimize=speed --cpu=native --enable-qsv --enable-vce)
  cd "${srcdir}/HandBrake"
  ./configure "${CONFIGURE_OPTIONS[@]}"
  make -C build
}

package_handbrake-svt-av1-essential-llvm-optimized(){
  pkgdesc="Multithreaded video transcoder with SVT-AV1-Essential (with HDR+PSY enhancements), LLVM-optimized"
  depends=("${_commondeps[@]}" "${_guideps[@]}" "${_implicitdeps[@]}")
  provides=(handbrake)
  conflicts=(handbrake)
  cd "HandBrake"
  make --directory="${srcdir}/HandBrake/build" DESTDIR="${pkgdir}" install
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/usr/bin/HandBrakeCLI"
}

package_handbrake-svt-av1-essential-llvm-optimized-cli(){
  pkgdesc="Multithreaded video transcoder with SVT-AV1-Essential (with HDR+PSY enhancements), LLVM-optimized (CLI)"
  depends=("${_commondeps[@]}" "${_implicitdeps[@]}")
  provides=(handbrake-cli)
  conflicts=(handbrake-cli)
  cd "HandBrake"
  install -D "${srcdir}/HandBrake/build/HandBrakeCLI" "${pkgdir}/usr/bin/HandBrakeCLI"
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
