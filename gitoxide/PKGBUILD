# Maintainer: Sebastian Thiel <sebastian.thiel@icloud.com>
pkgname=gitoxide
pkgver=0.46.0
pkgrel=1
makedepends=('rust' 'cargo')
arch=('x86_64' 'aarch64')
pkgdesc="A command-line application for interacting with git repositories"
license=('MIT OR Apache-2.0')
url='https://github.com/GitoxideLabs/gitoxide'
options=('strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GitoxideLabs/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('68a60cae90e0882cb3e1e699bc1c7e64902b632cc30209f60444c8ca8b2d820e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target
  export RUSTFLAGS="-C target-cpu=native -C opt-level=3 -C link-arg=-fuse-ld=lld"
  cargo build -r --frozen --no-default-features --bins --lib --features max-pure
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver" || exit
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/gix"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/ein"
}

# vim: ts=2 sw=2 et:
